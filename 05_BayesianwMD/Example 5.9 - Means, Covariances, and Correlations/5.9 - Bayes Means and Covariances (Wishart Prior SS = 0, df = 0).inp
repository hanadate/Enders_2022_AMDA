TITLE: 
example 5.8: bayesian means and covariances with wishart prior (ss = 0, df = 0);
DATA: 
file = employee.dat;
VARIABLE: 
names = employee team turnover male empower lmx worksat climate cohesion;
usevariables = worksat empower lmx;
missing = all(999);
ANALYSIS:
estimator = bayes;
fbiterations = 10000;
bseed = 90291;
MODEL:
! label covvariances;
worksat with empower (ws_em_c);
worksat with lmx (ws_lmx_c);
empower with lmx (em_lmx_c);
! label variances;
worksat (ws_var); empower (emp_var); lmx (lmx_var);
MODEL CONSTRAINT:
new(ws_sd, emp_sd, lmx_sd,ws_emp_r,ws_lmx_r,em_lmx_r);
ws_sd = sqrt(ws_var);
emp_sd = sqrt(emp_var);
lmx_sd = sqrt(lmx_var);
ws_emp_r = ws_em_c / (ws_sd * emp_sd);
ws_lmx_r = ws_lmx_c / (ws_sd * lmx_sd);
em_lmx_r = em_lmx_c / (emp_sd * lmx_sd);
OUTPUT:
patterns stdyx tech8;
MODEL PRIORS:
ws_em_c ~ IW(0,0);
ws_lmx_c ~ IW(0,0);
em_lmx_c ~ IW(0,0);
ws_var ~ IW(0,0);
emp_var ~ IW(0,0);
lmx_var ~ IW(0,0);
